'--------------------------------------------------------------
'Fitting Si + Al2O3 temperature standard
'Data collected with hot air blower so T(actual) < T(set) at high T
'File set up so that either topas v6 #list command or topas <v6 + command file can be used
'--------------------------------------------------------------
r_wp  14.7060581 r_exp  9.5411932 r_p  11.0801261 r_wp_dash  24.5432007 r_p_dash  23.5343473 r_exp_dash  15.9234662 weighted_Durbin_Watson  1.50046576 gof  1.54132275

iters 100000
chi2_convergence_criteria 0.001

#define topas_old_version 'include this if using v4 or v5

'{{{ Information on file names and temperatures in topas v6 #list style
#ifdef !topas_old_version 'i.e. use v6 as you haven't been told it's an old version of topas
Backup_INP
out_file = Concat(String(INP_File), ".INP"); 
num_runs 30
#list File_Name Temperature {
xye_files\576460-mac-001_reb_0003.xye	30
xye_files\576461-mac-001_reb_0003.xye	50
xye_files\576462-mac-001_reb_0003.xye	70
xye_files\576463-mac-001_reb_0003.xye	90
xye_files\576464-mac-001_reb_0003.xye	110
xye_files\576465-mac-001_reb_0003.xye	130
xye_files\576466-mac-001_reb_0003.xye	150
xye_files\576467-mac-001_reb_0003.xye	170
xye_files\576468-mac-001_reb_0003.xye	190
xye_files\576469-mac-001_reb_0003.xye	210
xye_files\576470-mac-001_reb_0003.xye	230
xye_files\576471-mac-001_reb_0003.xye	250
xye_files\576472-mac-001_reb_0003.xye	270
xye_files\576473-mac-001_reb_0003.xye	290
xye_files\576474-mac-001_reb_0003.xye	310
xye_files\576475-mac-001_reb_0003.xye	330
xye_files\576476-mac-001_reb_0003.xye	350
xye_files\576477-mac-001_reb_0003.xye	370
xye_files\576478-mac-001_reb_0003.xye	400
xye_files\576479-mac-001_reb_0003.xye	450
xye_files\576480-mac-001_reb_0003.xye	500
xye_files\576481-mac-001_reb_0003.xye	550
xye_files\576482-mac-001_reb_0003.xye	600
xye_files\576483-mac-001_reb_0003.xye	650
xye_files\576484-mac-001_reb_0003.xye	700
xye_files\576485-mac-001_reb_0003.xye	750
xye_files\576486-mac-001_reb_0003.xye	800
xye_files\576487-mac-001_reb_0003.xye	850
xye_files\576488-mac-001_reb_0003.xye	900
xye_files\576489-mac-001_reb_0003.xye	950    
}

prm !te =Temperature(Run_Number);:50.00000
macro filename { Filename(Run_Number) }
#endif
'}}}

'{{{ older version of topas
#ifdef topas_old_version
	#ifdef !GUI_LINES 'use these lines when in gui mode
	macro filename { xye_files\576460-mac-001_reb_0003.xye } 
	macro information {prm !te 30  } 
	#endif
	information

#endif
'}}}

xdd filename
	start_X 5
	finish_X 65
	x_calculation_step = Yobs_dx_at(Xo);
	'lambda derived on peak shape derived on 574646-mac-001_offline.xye on 21/4/2017.xye by John 2016
	'Si zero was -1.69258/1000 degrees
	lam ymin_on_ymax 0.00001 la 1.0 lo   0.8257653_8.44468382e-008 lh 0.1
	LP_Factor( 90) 'change the LP correction  or lh value if required
   prm zero -0.73189` th2_offset = (zero/1000);: -0.000731892355`
	bkg @  33.8497345` -20.6790085` -0.0215825246`  4.12571439` -7.26100925`  11.085209` -8.51176792`  0.504480555`  5.02091543` -5.47486731`  3.66723328` -2.31323425` -0.875693589`  3.93798196` -3.78164372`  2.5772305` -1.73983838`  0.593683324`

    str
		phase_name Al2O3
		a  lpa_1  4.759307`
		b =lpa_1;:4.759307`
		c lpc_1  12.992397`
		al 90.      
		be 90.      
		ga 120.     
		cell_volume vol_1  254.863`
		space_group "R-3cH"
		site Al1    x 0             y 0             z 0.148         occ Al+3 1.         beq bval_1  0.34729` 
		site O1     x 0.306         y 0             z 0.25          occ O-2  1.         beq bval_1  0.34729`
		scale  scal_1  6.13160855e-005`
		r_bragg  2.18302298
		Phase_Density_g_on_cm3( 3.98592`)
		'Sample size/strain include these lines for your sample
  		LVol_FWHM_CS_G_L(1, 281.81513`, 0.89, 392.90767`, csgc, 10000.00000`_LIMIT_MIN_0.3 , cslc, 441.48490`)
  		e0_from_Strain( 0.00003`, sgc, 0.00011`_LIMIT_MIN_0.0001, slc, 0.01530`)

	str
		phase_name silicon
		a  lpa_2  5.431042` '5.431597124
		b =lpa_2;    
		c =lpa_2;    
		al 90.      
		be 90.      
		ga 90.      
		cell_volume vol_2  160.195`
		space_group "Fd-3mS"
		site Si1    x 0             y 0             z 0             occ Si   1.         beq bval_2  0.38226`
		scale  scal_2  0.000103012159`
		r_bragg  2.01378994
		Phase_Density_g_on_cm3( 2.32901`)
		
for strs {
		'peak shape derived on 574646-mac-001_offline.xye on 21/4/2017
		'This is a fixed instrumental profile
		'Just use size/strain on your sample
		TCHZ_Peak_Type(!pku,-0.00072_0.00002,!pkv, -0.00008_0.00001,!pkw, -0.00080_0.00002,!pkz, 0.00087_0.00002,!pky, 0.01490_0.00011,!pkx, 0.00175_0.00002)
		Simple_Axial_Model(!axial, 2.12938_0.00228)
}

'{{{ Output lines here
#define write_out
#ifdef write_out
out "results.txt" append
Out_String(filename)
Out(te, " %11.5f")
Out(Get (r_wp), " %11.5f")
Out(lpa_1, " %11.5f", " %11.5f")
Out(lpc_1, " %11.5f", " %11.5f")
Out(vol_1, " %11.5f", " %11.5f")
Out(lpa_2, " %11.5f", " %11.5f")
Out(vol_2, " %11.5f", " %11.5f")
Out(scal_1, " %18.9f", " %18.9f")
Out(scal_2, " %18.9f", " %18.9f")
Out(bval_1, " %11.5f", " %11.5f")
Out(bval_2, " %11.5f", " %11.5f")
Out(te, " %11.5f\n")
#endif
'}}}	   

